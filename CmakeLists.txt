cmake_minimum_required(VERSION 3.22)

project(BeatCrafter VERSION 0.1.0)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

option(BUILD_STANDALONE "Build standalone application" ON)
option(BUILD_VST3 "Build VST3 plugin" ON)
option(BUILD_AU "Build AU plugin" OFF)

include(FetchContent)
FetchContent_Declare(
    JUCE
    GIT_REPOSITORY https://github.com/juce-framework/JUCE.git
    GIT_TAG        7.0.9
    GIT_SHALLOW    TRUE
    GIT_PROGRESS   TRUE
)
FetchContent_MakeAvailable(JUCE)

juce_add_plugin(BeatCrafter
    COMPANY_NAME "Innermost47"
    PLUGIN_MANUFACTURER_CODE BCrf
    PLUGIN_CODE BCr1
    
    FORMATS VST3 Standalone

    IS_SYNTH FALSE
    IS_MIDI_EFFECT TRUE
    NEEDS_MIDI_INPUT TRUE
    NEEDS_MIDI_OUTPUT TRUE
    PRODUCES_MIDI_OUTPUT TRUE

    PRODUCT_NAME "BeatCrafter"
    BUNDLE_ID "com.beatcrafter.beatcrafter"
)

target_sources(BeatCrafter
    PRIVATE
        src/PluginProcessor.cpp
        src/PluginEditor.cpp
        src/Core/Pattern.cpp
        src/Core/PatternEngine.cpp
        src/Core/IntensityMapper.cpp
        src/Core/StyleIntensityGenerator.cpp
        src/GUI/PatternGrid.cpp
        src/GUI/LookAndFeel.cpp
        src/GUI/SlotManager.cpp
)

target_compile_definitions(BeatCrafter
    PUBLIC
        JUCE_WEB_BROWSER=0
        JUCE_USE_CURL=0
        JUCE_VST3_CAN_REPLACE_VST2=0
        JUCE_DISPLAY_SPLASH_SCREEN=0
        JUCE_REPORT_APP_USAGE=0
)

target_link_libraries(BeatCrafter
    PRIVATE
        juce::juce_audio_basics
        juce::juce_audio_devices
        juce::juce_audio_formats
        juce::juce_audio_plugin_client
        juce::juce_audio_processors
        juce::juce_audio_utils
        juce::juce_core
        juce::juce_data_structures
        juce::juce_events
        juce::juce_graphics
        juce::juce_gui_basics
        juce::juce_gui_extra
    PUBLIC
        juce::juce_recommended_config_flags
        juce::juce_recommended_lto_flags
        juce::juce_recommended_warning_flags
)

if(MSVC)
    target_compile_options(BeatCrafter PRIVATE /W4)
else()
    target_compile_options(BeatCrafter PRIVATE -Wall -Wextra -Wpedantic)
endif()